{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using LinearAlgebra\n",
    "using Statistics\n",
    "using IterativeSolvers\n",
    "using Convex\n",
    "using SCS\n",
    "using COSMO\n",
    "using NLsolve\n",
    "using DifferentialEquations\n",
    "using SparseArrays\n",
    "using Random\n",
    "using GraphPlot\n",
    "using MathOptInterface\n",
    "using DelimitedFiles\n",
    "using Colors\n",
    "using Cairo, Compose\n",
    "using Optim\n",
    "\n",
    "using JLD2\n",
    "\n",
    "using LightGraphs\n",
    "using PyPlot\n",
    "PyPlot.svg(true);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function load_cdf(fname)\n",
    "    # load graph given in IEEE cdf format\n",
    "    buslines = []\n",
    "    branchlines = []\n",
    "    MVA_base = 0.0\n",
    "    \n",
    "    open(fname) do file\n",
    "        state = :nothing\n",
    "        for (i, line) in enumerate(eachline(file))\n",
    "            if i == 1\n",
    "                MVA_base = parse(Float64, line[32:37])\n",
    "            end\n",
    "            \n",
    "            # where in the file are we? set our state accordingly\n",
    "            if startswith(line, \"BUS DATA FOLLOWS\")\n",
    "                state = :bus\n",
    "                continue\n",
    "            end\n",
    "            \n",
    "            if startswith(line, \"BRANCH DATA FOLLOWS\")\n",
    "                state = :branch\n",
    "                continue\n",
    "            end   \n",
    "            \n",
    "            if startswith(line, \"-999\")\n",
    "                state = :nothing\n",
    "                continue\n",
    "            end\n",
    "            \n",
    "            # separate data \n",
    "            if state == :bus\n",
    "                push!(buslines, line)\n",
    "            elseif state == :branch\n",
    "                push!(branchlines, line)\n",
    "            end\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    # relevant bus data\n",
    "    io = IOBuffer(join(buslines, \"\\n\"))\n",
    "    busdata = readdlm(io)\n",
    "    close(io)\n",
    "    \n",
    "    voltage = busdata[:,8]\n",
    "    angle = busdata[:,9]\n",
    "    load_mw = busdata[:,10]\n",
    "    load_mvar = busdata[:,11]\n",
    "    gen_mw = busdata[:,12]\n",
    "    gen_mvar = busdata[:,13]\n",
    "    base_kv = busdata[:,14]\n",
    "    \n",
    "    # relevant branch data\n",
    "    io = IOBuffer(join(branchlines, \"\\n\"))\n",
    "    branchdata = readdlm(io, Any)\n",
    "    close(io)\n",
    "    \n",
    "    branch_a = branchdata[:,1]\n",
    "    branch_b = branchdata[:,2]\n",
    "    branch_type = branchdata[:,6]\n",
    "    \n",
    "    branch_R = branchdata[:,7]\n",
    "    branch_X = branchdata[:,8]\n",
    "    \n",
    "    branch_B = branch_X./(branch_R.^2 + branch_X.^2)\n",
    "    branch_G = branch_R./(branch_R.^2 + branch_X.^2)\n",
    "    \n",
    "    # construct graph\n",
    "    g = Graph(length(voltage))\n",
    "    Bs = Float64[]\n",
    "    Gs = Float64[]\n",
    "    for (a, b, t, B, G) in zip(branch_a, branch_b, branch_type, branch_B, branch_G)\n",
    "        if t == 0 # transmission line\n",
    "            add_edge!(g, a, b)\n",
    "            push!(Bs, B*abs(voltage[a]*voltage[b]))\n",
    "            push!(Gs, G*abs(voltage[a]*voltage[b]))\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    P = gen_mw - load_mw\n",
    "    P .-= mean(P)\n",
    "    \n",
    "    Q = gen_mvar - load_mvar\n",
    "    \n",
    "    g, Bs, Gs, P/MVA_base, Q/MVA_base, angle*π/180\n",
    "end\n",
    "\n",
    "g, B, G, P, Q, δnet = load_cdf(\"ieee14cdf.txt\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "sum(Q), sum(P)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "N = nv(g)\n",
    "E = incidence_matrix(g; oriented=true)\n",
    "is_connected(g)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function distance_matrix(g)\n",
    "    dists = zeros(N, N)\n",
    "\n",
    "    for i=1:N\n",
    "        sp = LightGraphs.dijkstra_shortest_paths(g, [i])\n",
    "        dists[i,:] .= sp.dists\n",
    "    end\n",
    "    \n",
    "    dists\n",
    "end\n",
    "\n",
    "dists = distance_matrix(g);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function optimize_graph(g, B, P, α; δ0=1e-6randn(N), GI=0.0)\n",
    "    N = nv(g)\n",
    "\n",
    "    E = incidence_matrix(g; oriented=true)\n",
    "\n",
    "    # laplacian\n",
    "    L = Symmetric(E*diagm(0=>B)*E')\n",
    "\n",
    "    L_sp = dropzeros(sparse(L))\n",
    "\n",
    "    function f_steady_state(F, δ)\n",
    "        F .= P - E*(B.*sin.(E'*δ))\n",
    "    end\n",
    "\n",
    "    # initial condition\n",
    "#     δ0 = 1e-6randn(nv(g))\n",
    "    sln = nlsolve(f_steady_state, δ0; autodiff=:forward)\n",
    "\n",
    "    # order parameter at the fixed point\n",
    "    R0 = abs2(mean(exp.(1im*sln.zero)))\n",
    "    \n",
    "    @show R0, converged(sln)\n",
    "    \n",
    "    # check by solving ODE\n",
    "    function f_kuramoto(du, u, p, t)\n",
    "        f_steady_state(du, u)\n",
    "    end\n",
    "\n",
    "    # Noisy extension\n",
    "    δbar = sln.zero\n",
    "    \n",
    "    # construct S matrix\n",
    "    h = complete_graph(nv(g))\n",
    "    Eh = incidence_matrix(h; oriented=true)\n",
    "    cosδbar = cos.(Eh'*δbar)\n",
    "    # cosδbar = ones(ne(h))\n",
    "\n",
    "    # Laplacian of the corresponding complete graph\n",
    "    S = Symmetric(-Eh*diagm(0=>cosδbar)*Eh'/nv(g)^2)\n",
    "\n",
    "    # weighted Laplacian expanded around fixed point\n",
    "    B_fp = cos.(E'*δbar).*B\n",
    "    L_fp = Symmetric(E*diagm(0 => B_fp)*E')\n",
    "    \n",
    "    # optimize!\n",
    "    if GI == 0.0\n",
    "        Eq = Convex.Semidefinite(2N)\n",
    "        C = Convex.Semidefinite(N)\n",
    "\n",
    "        Mmat = [zeros(N,N)  I\n",
    "                -L_fp  -α*I]\n",
    "\n",
    "        Rmat = [zeros(N,N) zeros(N,N)\n",
    "                zeros(N,N) -C]\n",
    "\n",
    "        problem = Convex.maximize(tr(S*Eq[1:N,1:N]), [\n",
    "    #             L_fp*Eq + Eq*L_fp == C, \n",
    "                Mmat*Eq + Eq*Mmat' == Rmat,\n",
    "                Eq*ones(2N) == zeros(2N),\n",
    "        #         sum(Eq) == 0.0,\n",
    "                diag(C, 0) == 1.0,\n",
    "        #         tr(C) == nv(g),\n",
    "                ])\n",
    "\n",
    "        Convex.solve!(problem, () -> COSMO.Optimizer(verbose=false, \n",
    "                eps_abs=1e-7, eps_rel=1e-7, max_iter=200000))\n",
    "    else\n",
    "        # optimize including integral control\n",
    "        Eq = Convex.Semidefinite(2N)\n",
    "        C = Convex.Semidefinite(N)\n",
    "\n",
    "        Mmat = [zeros(N,N)                     I         \n",
    "                (-L_fp - GI*ones(N,N)/N)       -α*I]\n",
    "\n",
    "        Rmat = [zeros(N,N) zeros(N,N)\n",
    "                zeros(N,N)    -C      ]\n",
    "\n",
    "        problem = Convex.maximize(tr(S*Eq[1:N,1:N]), [\n",
    "    #             L_fp*Eq + Eq*L_fp == C, \n",
    "                Mmat*Eq + Eq*Mmat' == Rmat,\n",
    "                C*ones(N) == zeros(N),\n",
    "#                 Eq[1:2N,1:2N]*ones(2N) == zeros(2N),\n",
    "        #         sum(Eq) == 0.0,\n",
    "                diag(C, 0) == 1.0,\n",
    "        #         tr(C) == nv(g),\n",
    "                ])\n",
    "\n",
    "        Convex.solve!(problem, () -> COSMO.Optimizer(verbose=false, \n",
    "                eps_abs=1e-7, eps_rel=1e-7, max_iter=200000))        \n",
    "    end\n",
    "    \n",
    "    @show problem.optval, problem.status\n",
    "    \n",
    "    if (problem.status == MathOptInterface.OPTIMAL) && converged(sln)\n",
    "        C.value, Eq.value, S, L_fp, R0, problem.optval, δ0, δbar, f_kuramoto\n",
    "    else\n",
    "        [NaN], [NaN], [NaN], [NaN], NaN, NaN, NaN, NaN, nothing\n",
    "    end\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "Copt, Eopt, S, L_fp, R0, optval, δ0, δbar, f_kuramoto = optimize_graph(g, B, P, 1.0; GI=2.0, δ0=0.5π*randn(N))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "fig, axs = subplots(1, 3, figsize=(8, 2.5))\n",
    "\n",
    "ax = axs[1]\n",
    "cmin, cmax = abs(minimum(Copt)), abs(maximum(Copt))\n",
    "cm = maximum([cmin, cmax])\n",
    "sc = ax.matshow(Copt, cmap=\"RdBu\", vmin=-cm, vmax=cm)\n",
    "fig.colorbar(sc, ax=ax, fraction=0.046, pad=0.04, label=\"noise covariance matrix C\")\n",
    "\n",
    "ax = axs[2]\n",
    "cmin, cmax = abs(minimum(Eopt)), abs(maximum(Eopt))\n",
    "cm = maximum([cmin, cmax])\n",
    "sc = ax.matshow(Eopt, cmap=\"RdBu\", vmin=-cm, vmax=cm)\n",
    "fig.colorbar(sc, ax=ax, fraction=0.046, pad=0.04, label=\"perturbation covariance ⟨εεᵀ⟩\")\n",
    "\n",
    "ax = axs[3]\n",
    "\n",
    "CD = Copt./dists\n",
    "CD[.!isfinite.(CD)] .= 0.0\n",
    "cmin, cmax = abs(minimum(CD)), abs(maximum(CD))\n",
    "cm = maximum([cmin, cmax])\n",
    "sc = ax.matshow(CD, cmap=\"RdBu\", vmin=-cm, vmax=cm)\n",
    "fig.colorbar(sc, ax=ax, fraction=0.046, pad=0.04, label=\"C/D\")\n",
    "\n",
    "fig.tight_layout()"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "@show sum(abs.(Copt))/N^2\n",
    "@show sum(Eopt)\n",
    "@show norm(Eopt[1:2N,1:2N]*ones(2N))\n",
    "\n",
    "# optimal solution\n",
    "optval"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# compare to uniform noise\n",
    "C_unif = ((1 + 1/nv(g))I - ones(nv(g), nv(g))/nv(g))\n",
    "\n",
    "E_uf = lyap(Array(L_fp), -C_unif)\n",
    "\n",
    "optval_uniform = tr(E_uf*S)\n",
    "\n",
    "@show optval_uniform\n",
    "@show optval_uniform/optval"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# jldsave(\"data/ieee_$(N)_fix_Cii.jld2\"; g, K, δ0, δbar, R0, Copt, Eopt, optval, optval_uniform)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# calculate for various α\n",
    "αs = 10.0.^LinRange(-2, 2, 101)\n",
    "Copts = []\n",
    "Eopts = []\n",
    "Ss = []\n",
    "L_fps = []\n",
    "R0s = []\n",
    "δ0s = []\n",
    "δbars = []\n",
    "optvals = []\n",
    "\n",
    "δ0 = zeros(nv(g))\n",
    "for α in αs\n",
    "    Copt, Eopt, S, L_fp, R0, optval, δ0, δbar, f_kuramoto = optimize_graph(g, B, P, α; δ0=δ0)\n",
    "    \n",
    "    push!(Copts, Copt)\n",
    "    push!(Eopts, Eopt)\n",
    "    push!(Ss, S)\n",
    "    push!(L_fps, L_fp)\n",
    "    push!(R0s, R0)\n",
    "    push!(optvals, optval)\n",
    "    push!(δ0s, δ0)\n",
    "    push!(δbars, δbar)\n",
    "    \n",
    "    @show α, optval\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# jldsave(\"data/powergrid_ieee_$(N)_fix_Cii.jld2\"; g, αs, Copts, Eopts, Ss, L_fps, R0s, δ0s, δbars, optvals)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "fig, ax = subplots()\n",
    "\n",
    "Cnorms = [mean(abs.(C)) for C in Copts]\n",
    "\n",
    "ax.semilogx(αs, optvals)\n",
    "\n",
    "ax2 = ax.twinx()\n",
    "ax2.semilogx(αs, Cnorms, \"C1\")\n",
    "\n",
    "ax.set_xlabel(\"α\")\n",
    "ax.set_ylabel(\"tr(HE)\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# calculate for various GIs\n",
    "α = 0.1\n",
    "GIs = 10.0.^LinRange(-2, 2, 11)\n",
    "Copts = []\n",
    "Eopts = []\n",
    "Ss = []\n",
    "L_fps = []\n",
    "R0s = []\n",
    "δ0s = []\n",
    "δbars = []\n",
    "optvals = []\n",
    "\n",
    "δ0 = zeros(nv(g))\n",
    "for GI in GIs\n",
    "    Copt, Eopt, S, L_fp, R0, optval, δ0, δbar, f_kuramoto = optimize_graph(g, B, P, α; δ0=δ0, GI=GI)\n",
    "    \n",
    "    push!(Copts, Copt)\n",
    "    push!(Eopts, Eopt)\n",
    "    push!(Ss, S)\n",
    "    push!(L_fps, L_fp)\n",
    "    push!(R0s, R0)\n",
    "    push!(optvals, optval)\n",
    "    push!(δ0s, δ0)\n",
    "    push!(δbars, δbar)\n",
    "    \n",
    "    @show GI, optval\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# jldsave(\"data/powergrid_control_ieee_$(N)_fix_Cii.jld2\"; g, α, GIs, Copts, Eopts, Ss, L_fps, R0s, δ0s, δbars, optvals)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "fig, ax = subplots()\n",
    "\n",
    "Cnorms = [mean(abs.(C)) for C in Copts]\n",
    "\n",
    "ax.semilogx(GIs, optvals)\n",
    "\n",
    "ax2 = ax.twinx()\n",
    "ax2.semilogx(GIs, Cnorms, \"C1\")\n",
    "\n",
    "ax.set_xlabel(\"G_I\")\n",
    "ax.set_ylabel(\"tr(HE)\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# find a particular realization of the noise input\n",
    "function G_from_C(C)\n",
    "    U, Σ, V = svd(C)\n",
    "    U*diagm(0 => sqrt.(Σ))*U'\n",
    "end\n",
    "\n",
    "G = G_from_C(Copt)\n",
    "η = G*randn(nv(g));"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# run a SDE solution to check results\n",
    "\n",
    "function solve_sde(σ, δ0, G; tspan=(0.0, 3000.0), dt=0.01)\n",
    "    # σ is the noise strength (multiplies the noise matrix)\n",
    "    function g_kuramoto(du, u, p, t)\n",
    "        du .= 1/sqrt(2)*σ*G\n",
    "    end\n",
    "    \n",
    "    prob = SDEProblem(f_kuramoto, g_kuramoto, δ0, tspan; noise_rate_prototype=zeros(size(G)...))\n",
    "    sdesln = solve(prob, EM(), dt=dt)\n",
    "end\n",
    "\n",
    "function cumtrapz_avg(t::T, Y::T) where {T <: AbstractVector}\n",
    "    # Estimates the cumulative time average integral 1/T ∫₀ᵀ f(t) dt using the trapezoid rule\n",
    "    # where time points are in t and corresponding samples of f are in Y\n",
    "    \n",
    "    # Check matching vector length\n",
    "    @assert length(t) == length(Y)\n",
    "    \n",
    "    # Initialize Output\n",
    "    out = similar(t)\n",
    "    out[1] = 0.0\n",
    "    # Iterate over arrays\n",
    "    for i in 2:length(t)\n",
    "        out[i] = out[i-1] + 0.5*(t[i] - t[i-1])*(Y[i] + Y[i-1])\n",
    "    end\n",
    "    out[2:end] ./= (t[2:end] .- t[1])\n",
    "    out[1] = out[2]\n",
    "\n",
    "    out\n",
    "end\n",
    "\n",
    "function Rsqr_from_sde(sdesln)\n",
    "    # numerically integrate and average\n",
    "    Rsqrs = [abs(mean(exp.(1im*u)))^2 for u in sdesln.u]\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "σ = 0.25\n",
    "G = G_from_C(Copt)\n",
    "\n",
    "sdesln = solve_sde(σ, δbar, G; tspan=(0.0, 5000.0))\n",
    "Rsqrs_sde = Rsqr_from_sde(sdesln);\n",
    "Rsqrs_avg = cumtrapz_avg(sdesln.t, Rsqrs_sde);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "G_unif = G_from_C(C_unif)\n",
    "\n",
    "sdesln_unif = solve_sde(σ, δbar, G_unif; tspan=(0.0, 5000.0))\n",
    "Rsqrs_sde_unif = Rsqr_from_sde(sdesln_unif);\n",
    "Rsqrs_avg_unif = cumtrapz_avg(sdesln_unif.t, Rsqrs_sde_unif);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "ts = sdesln.t\n",
    "ts_unif = sdesln_unif.t\n",
    "\n",
    "# jldsave(\"data/ieee_$(N)_timeseries.jld2\"; Rsqrs_avg, Rsqrs_avg_unif, ts, ts_unif, σ, G)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "fig, ax = subplots()\n",
    "# ax.axhline(R0, ls=\":\", label=\"R₀²\")\n",
    "# ax.axhline(R0 + 0.5σ^2*optval, label=\"R₀² + 0.5σ² tr(SE)_opt\", color=\"C1\", ls=\":\")\n",
    "# ax.axhline(R0 + 0.5σ^2*optval_uniform, label=\"R₀² + 0.5σ² tr(SE)_unif\", color=\"C2\", ls=\":\")\n",
    "# ax.axhline(R0 - σ^2*problem.optval)\n",
    "\n",
    "# ax.set_ylim(R0 - 5σ^2*abs(optval_uniform), R0 + 5σ^2*abs(optval_uniform))\n",
    "\n",
    "# ax.plot(Rsqrs_sde, alpha=0.5)\n",
    "ax.plot(sdesln.t, Rsqrs_avg, color=\"C1\", label=\"⟨R²⟩ optimal\")\n",
    "ax.plot(sdesln_unif.t, Rsqrs_avg_unif, color=\"C2\", label=\"⟨R²⟩ uniform\")\n",
    "\n",
    "# ax.set_xlim(150000, 200000)\n",
    "\n",
    "ax.legend()\n",
    "ax.set_xlabel(\"time t\")\n",
    "ax.set_ylabel(\"order parameter\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "cm = colormap(\"RdBu\")[15:85]\n",
    "\n",
    "# i = 19\n",
    "i = 6\n",
    "cols = Int64.(round.(70*(Copt[i,:]/2 .+ 0.5)))\n",
    "cols[cols .== 0] .= 1\n",
    "\n",
    "# edgecols = [colorant\"white\" for i=1:N]\n",
    "# cols[i] = 35\n",
    "\n",
    "layout=(args...)->spring_layout(args...; C=0.5, MAXITER=1500)\n",
    "p = gplot(g, layout=layout, nodefillc=cm[cols], nodelabel=1:N, edgelinewidth=K, EDGELINEWIDTH=3)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Compose.draw(SVG(\"ieee$(N).svg\"), p)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.7.2",
   "language": "julia",
   "name": "julia-1.7"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.7.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
